config {
    type: "assertion",
    schema: dataform.projectConfig.vars.QUALITY_DATASET,
    disabled: false,
    description: "Ensures that int_ga4_sessions, ga4_sessions, and ga4_events are up-to-date with the latest GA4 raw event table.",
    dependencies: [
      {name: "ga4_events"},  
      {name: "int_ga4_sessions"}, 
      {name: "ga4_sessions"}
    ],
    tags: [dataform.projectConfig.vars.GA4_DATASET, "assertions"]
}

WITH max_dates AS (
  SELECT
    'int_ga4_sessions' AS table_name,
    MAX(session_date) AS max_date
  FROM
    ${ref("int_ga4_sessions")}
  UNION ALL
  SELECT
    'ga4_sessions' AS table_name,
    MAX(session_date) AS max_date
  FROM
    ${ref("ga4_sessions")}
  UNION ALL
  SELECT
    'ga4_events' AS table_name,
    MAX(event_date) AS max_date
  FROM
    ${ref("ga4_events")}
),
latest_event_date AS (
  SELECT
    MAX(PARSE_DATE('%Y%m%d', event_date)) AS max_event_date
  FROM
    ${ref("events_*")}
  WHERE
    CONTAINS_SUBSTR(_TABLE_SUFFIX, 'intraday') IS FALSE
)

SELECT
  table_name
FROM
  max_dates
JOIN
  latest_event_date
ON
  max_dates.max_date != latest_event_date.max_event_date
